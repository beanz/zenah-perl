#!/usr/bin/perl -w
use strict;
use ZenAH::Google::Chart;
use RRDs;

# google-chart-from-rrd inside.rrd|Inside\ Temp outside.rrd|Outside\ Temp

my $span = '1d';
my $rrdres = 3600;
my $start = int(time/$rrdres)*$rrdres;
my %chart_data;
my %legend;
foreach my $file (@ARGV) {
  my $side = 'left';
  if ($file =~ s/!$//) {
    $side = 'right';
  }
  my $legend;
  if ($file =~ s/\|([^|]+)$//) {
    $legend = $1;
  } else {
    $legend = $file;
    $legend =~ s!^.*\/!!;
    $legend =~ s!\.rrd$!!;
  }
  push @{$legend{$side}}, $legend;
  my $cf = 'AVERAGE';
  if ($file =~ s/:([^\/:|]+)(?:\|[^|]*)?$//) {
    $cf = $1;
  }
  print $file, ' ', $cf, ' ', $legend, "\n";
  my ($start,$step,$names,$data) =
    RRDs::fetch $file, $cf, '-r', $rrdres, '-e', $start, '-s', 'e-'.$span;

  my $index = 0;
  foreach my $line (@$data) {
    unless (defined $chart_data{$side}->[$index]) {
      push @{$chart_data{$side}->[$index]}, $start;
    }
    foreach my $val (@$line) {
      push @{$chart_data{$side}->[$index]}, $val;
    }
    $start += $step;
    $index++;
  }
}
my @legend = ();
push @legend, @{$legend{'left'}||[]};
push @legend, @{$legend{'right'}||[]};

#use Data::Dumper;
#print Data::Dumper->Dump([\@chart_data], [qw/d/]);
my $c = ZenAH::Google::Chart->new({width => 680, height => 300,
                                   type => 'lc',

                                   data => $chart_data{'left'},
                                   min => 0,
                                   legend => $legend{'left'},

                                   right_data => $chart_data{'right'},
                                   right_min => 0,
                                   right_legend => $legend{'right'},

                                   xfmt => '!%H', xmod => 6,
                                   colours => ['ff0000', '00ff00', '00ffff'],
                                   fill => 1});
print $c->url,"\n";
